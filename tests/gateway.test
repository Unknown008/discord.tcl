# Commands covered: GetGateway
#
# gateway.test --
#
#       This file contains tests for commands in the discord::gateway namespace.
#       Sourcing this file into Tcl runs the tests and generates output for
#       errors. No output means no errors were found.
#
# Copyright (c) 2016, Yixin Zhang
#
# See the file "LICENSE" for information on usage and redistribution of this
# file.

package require tcltest
namespace import tcltest::*

# Add parent directory to auto_path to load the discord package.

set scriptDir [file dirname [info script]]
lappend ::auto_path "$scriptDir/../"
package require discord

# Suppress all logging.

${discord::gateway::log}::disable emergency

set testUrl "wss://discord.test"
set originalUrl $discord::ApiBaseUrl

test GetGateway-1.1 {Retrieve uncached URL, no cached URL} -body {
    discord::gateway::GetGateway 0
} -cleanup {
    set discord::gateway::GatewayUrl ""
} -match regexp -result {^wss://.+$} 

test GetGateway-1.2 {Retrieve cached URL, has cached URL} -setup {
    set discord::gateway::GatewayUrl $testUrl
} -body {
    discord::gateway::GetGateway
} -cleanup {
    set discord::gateway::GatewayUrl ""
} -result $testUrl

test GetGateway-1.3 {Retrieve uncached URL, has cached URL} -setup {
    set discord::gateway::GatewayUrl $testUrl
} -body {
    set url [discord::gateway::GetGateway 0]
    return [expr {$url ne $testUrl}]
} -cleanup {
    set discord::gateway::GatewayUrl ""
} -result 1

test GetGateway-1.4 {Retrieve cached URL, no cached URL} -body {
    discord::gateway::GetGateway
} -cleanup {
    set discord::gateway::GatewayUrl ""
} -match regexp -result {^wss://.+$}

test GetGateway-2.1 {Invalid base API URL} -setup {
    set discord::ApiBaseUrl "tcl://discord.tcl"
} -body {
    if {[catch {discord::gateway::GetGateway} url options]} {
        return -options $options $url
    }
} -cleanup {
    set discord::gateway::GatewayUrl ""
    set discord::ApiBaseUrl $originalUrl
} -returnCodes error -result {Unsupported URL type "tcl"}

test GetGateway-2.2 {HTTP status timeout} -setup {
    set clients [list]
    proc HandleClients { channel clientAddr clientPort } {
        lappend ::clients $channel
        chan configure $channel -blocking 0 -buffering none
        chan event $channel readable [list apply { { channel } {
                    if {![chan eof $channel]} {
                        chan gets $channel
                    }
                } } $channel]
    }
    set sock [socket -server HandleClients -myaddr localhost 0]
    lassign [chan configure $sock -sockname] address hostname port
    set discord::ApiBaseUrl "http://${hostname}:$port"
} -body {
    if {[catch {discord::gateway::GetGateway 0 -timeout 1500} url options]} {
        return -options $options $url
    }
} -cleanup {
    set discord::ApiBaseUrl $originalUrl
    foreach channel $clients {
        if {[llength [file channels $channel]] > 0} {
            chan close $channel
        }
    }
    chan close $sock
} -returnCodes error -result timeout

test GetGateway-2.3 {Connection refused} -setup {
    # Connect to a port that is not opened by binding to it ourselves and then
    # closing the socket.
    set sock [socket -server {} -myaddr localhost 0]
    lassign [chan configure $sock -sockname] address hostname port
    chan close $sock
    set discord::ApiBaseUrl "http://${hostname}:$port"
} -body {
    if {[catch {discord::gateway::GetGateway 0} url options]} {
        return -options $options $url
    }
} -cleanup {
    set discord::ApiBaseUrl $originalUrl
} -returnCodes error -result {connect failed connection refused}

test GetGateway-2.4 {HTTP base API URL instead of HTTPS} -setup {
    set discord::ApiBaseUrl [regsub {^https} $discord::ApiBaseUrl http]
} -body {
    discord::gateway::GetGateway
} -cleanup {
    set discord::gateway::GatewayUrl ""
    set discord::ApiBaseUrl $originalUrl
} -returnCodes error -result 301

test GetGateway-2.5 {HTTP status eof} -setup {
    set sock [socket -server [list apply { { channel args } {
                chan puts $channel {}
                chan close $channel
            } }] -myaddr localhost 0]
    lassign [chan configure $sock -sockname] address hostname port
    set discord::ApiBaseUrl "http://${hostname}:$port"
} -body {
    if {[catch {discord::gateway::GetGateway} url options]} {
        return -options $options $url
    }
} -cleanup {
    set discord::ApiBaseUrl $originalUrl
    chan close $sock
} -returnCodes error -result eof

test GetGateway-2.6 {Invalid JSON payload} -setup {
    set sock [socket -server [list apply { { channel args } {
                chan configure $channel -blocking 0 -buffering full
                chan event $channel readable [list apply { { channel } {
                            chan read $channel
                            chan puts $channel [join {
                                        {HTTP/1.1 200 OK}
                                        {}
                                        {Not JSON}
                                    } "\r\n"]
                            chan close $channel
                        } } $channel]
            } }] -myaddr localhost 0]
    lassign [chan configure $sock -sockname] address hostname port
    set discord::ApiBaseUrl "http://${hostname}:$port"
} -body {
    if {[catch {discord::gateway::GetGateway} url options]} {
        return -options $options $url
    }
} -cleanup {
    set discord::ApiBaseUrl $originalUrl
    chan close $sock
} -returnCodes error \
    -result {unexpected token "END" at position 0; expecting VALUE}

test GetGateway-2.7 {JSON payload with no url field} -setup {
    set sock [socket -server [list apply { { channel args } {
                chan configure $channel -blocking 0 -buffering full
                chan event $channel readable [list apply { { channel } {
                            chan read $channel
                            chan puts $channel [join {
                                        {HTTP/1.1 200 OK}
                                        {}
                                        {{"notUrl" : 1}}
                                    } "\r\n"]
                            chan close $channel
                        } } $channel]
            } }] -myaddr localhost 0]
    lassign [chan configure $sock -sockname] address hostname port
    set discord::ApiBaseUrl "http://${hostname}:$port"
} -body {
    if {[catch {discord::gateway::GetGateway} url options]} {
        return -options $options $url
    }
} -cleanup {
    set discord::ApiBaseUrl $originalUrl
    chan close $sock
} -returnCodes error -result {key "url" not known in dictionary}
