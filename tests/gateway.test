# Commands covered: GetGateway
#
# gateway.test --
#
#       This file contains tests for commands in the discord::gateway namespace.
#       Sourcing this file into Tcl runs the tests and generates output for
#       errors. No output means no errors were found.
#
# Arguments:
#       apiBaseUrl  (optional) URL to connect to for retrieving the Gateway API
#                   URL. Defaults to the variable discord::ApiBaseUrl.
#
# Copyright (c) 2016, Yixin Zhang
#
# See the file "LICENSE" for information on usage and redistribution of this
# file.

package require tcltest
namespace import tcltest::*

set scriptDir [file dirname [info script]]

# Add parent directory to auto_path to load the discord package.

lappend ::auto_path "$scriptDir/../"
package require discord

set localServerScript "$scriptDir/local_server.tcl"
source $localServerScript

# Suppress all logging.

${discord::gateway::log}::disable emergency

set testUrl "wss://discord.test"
set originalUrl $discord::ApiBaseUrl

lassign $argv apiBaseUrl
if {$apiBaseUrl eq {}} {
    set apiBaseUrl $::discord::ApiBaseUrl
}

# outputVar will contain the local server URL.
proc StartLocalServer { outputVar {timeout {3000}} {protocol https} \
        {scripts {}} args } {
    variable localServerScript
    lassign [chan pipe] rdPipe wrPipe
    chan configure $rdPipe -blocking 0 -buffering line
    chan event $rdPipe readable [list apply { { outputVar rdPipe wrPipe } {
                if {![chan eof $rdPipe]} {
                    upvar #0 $outputVar output
                    set output [chan gets $rdPipe]
                }
                chan close $rdPipe
                chan close $wrPipe
            } } $outputVar $rdPipe $wrPipe]
    exec {*}[auto_execok tclsh] $localServerScript $timeout $protocol {} \
            $scripts $args >@ $wrPipe &
}

test GetGateway-1.1 {Retrieve uncached URL, no cached URL} -setup {
    set writeScript [list apply { {url channel } {
                chan puts $channel [join [list \
                        "HTTP/1.1 200 OK" \
                        "" \
                        "\{\"url\":\"$url\"\}"] "\r\n"]
                chan close $channel
            } } $testUrl]
    set scripts [list {} $writeScript]
    StartLocalServer url 1000 https $scripts -blocking 0 -buffering full
    vwait url
    set ::discord::ApiBaseUrl $url
} -body {
    ::discord::gateway::GetGateway 0
} -cleanup {
    set ::discord::gateway::GatewayUrl ""
    set ::discord::ApiBaseUrl $originalUrl
} -result $testUrl

test GetGateway-1.2 {Retrieve cached URL, has cached URL} -setup {
    set discord::gateway::GatewayUrl $testUrl
} -body {
    discord::gateway::GetGateway
} -cleanup {
    set discord::gateway::GatewayUrl ""
} -result $testUrl

test GetGateway-1.3 {Retrieve uncached URL, has cached URL} -setup {
    set discord::gateway::GatewayUrl $testUrl
} -body {
    set url [discord::gateway::GetGateway 0]
    return [expr {$url ne $testUrl}]
} -cleanup {
    set discord::gateway::GatewayUrl ""
} -result 1

test GetGateway-1.4 {Retrieve cached URL, no cached URL} -setup {
    set writeScript [list apply { {url channel } {
                chan puts $channel [join [list \
                        "HTTP/1.1 200 OK" \
                        "" \
                        "\{\"url\":\"$url\"\}"] "\r\n"]
                chan close $channel
            } } $testUrl]
    set scripts [list {} $writeScript]
    StartLocalServer url 1000 https $scripts -blocking 0 -buffering full
    vwait url
    set ::discord::ApiBaseUrl $url
} -body {
    discord::gateway::GetGateway
} -cleanup {
    set discord::gateway::GatewayUrl ""
    set ::discord::ApiBaseUrl $originalUrl
} -match regexp -result $testUrl

test GetGateway-2.1 {Invalid base API URL} -setup {
    set discord::ApiBaseUrl "tcl://discord.tcl"
} -body {
    discord::gateway::GetGateway
} -cleanup {
    set discord::gateway::GatewayUrl ""
    set discord::ApiBaseUrl $originalUrl
} -returnCodes error -result {Unsupported URL type "tcl"}

test GetGateway-2.2 {HTTP status timeout} -setup {
    set clients [list]
    proc HandleClients { channel clientAddr clientPort } {
        lappend ::clients $channel
        chan configure $channel -blocking 0 -buffering none
        chan event $channel readable [list apply { { channel } {
                    if {![chan eof $channel]} {
                        chan gets $channel
                    }
                } } $channel]
    }
    set sock [socket -server HandleClients -myaddr localhost 0]
    lassign [chan configure $sock -sockname] address hostname port
    set discord::ApiBaseUrl "http://${hostname}:$port"
} -body {
    discord::gateway::GetGateway 0 -timeout 1500
} -cleanup {
    set discord::ApiBaseUrl $originalUrl
    foreach channel $clients {
        if {[llength [file channels $channel]] > 0} {
            chan close $channel
        }
    }
    chan close $sock
} -returnCodes error -result timeout

test GetGateway-2.3 {Connection refused} -setup {
    # Connect to a port that is not opened by binding to it ourselves and then
    # closing the socket.
    set sock [socket -server {} -myaddr localhost 0]
    lassign [chan configure $sock -sockname] address hostname port
    chan close $sock
    set discord::ApiBaseUrl "http://${hostname}:$port"
} -body {
    discord::gateway::GetGateway 0
} -cleanup {
    set discord::ApiBaseUrl $originalUrl
} -returnCodes error -result {connect failed connection refused}

test GetGateway-2.4 {HTTP base API URL instead of HTTPS} -setup {
    set discord::ApiBaseUrl [regsub {^https} $discord::ApiBaseUrl http]
} -body {
    discord::gateway::GetGateway
} -cleanup {
    set discord::gateway::GatewayUrl ""
    set discord::ApiBaseUrl $originalUrl
} -returnCodes error -result 301

test GetGateway-2.5 {HTTP status eof} -setup {
    set sock [socket -server [list apply { { channel args } {
                chan puts $channel {}
                chan close $channel
            } }] -myaddr localhost 0]
    lassign [chan configure $sock -sockname] address hostname port
    set discord::ApiBaseUrl "http://${hostname}:$port"
} -body {
    discord::gateway::GetGateway
} -cleanup {
    set discord::ApiBaseUrl $originalUrl
    chan close $sock
} -returnCodes error -result eof

test GetGateway-2.6 {Invalid JSON payload} -setup {
    set sock [socket -server [list apply { { channel args } {
                chan configure $channel -blocking 0 -buffering full
                chan event $channel readable [list apply { { channel } {
                            chan read $channel
                            chan puts $channel [join {
                                        {HTTP/1.1 200 OK}
                                        {}
                                        {Not JSON}
                                    } "\r\n"]
                            chan close $channel
                        } } $channel]
            } }] -myaddr localhost 0]
    lassign [chan configure $sock -sockname] address hostname port
    set discord::ApiBaseUrl "http://${hostname}:$port"
} -body {
    discord::gateway::GetGateway
} -cleanup {
    set discord::ApiBaseUrl $originalUrl
    chan close $sock
} -returnCodes error \
    -result {unexpected token "END" at position 0; expecting VALUE}

test GetGateway-2.7 {JSON payload with no url field} -setup {
    set sock [socket -server [list apply { { channel args } {
                chan configure $channel -blocking 0 -buffering full
                chan event $channel readable [list apply { { channel } {
                            chan read $channel
                            chan puts $channel [join {
                                        {HTTP/1.1 200 OK}
                                        {}
                                        {{"notUrl" : 1}}
                                    } "\r\n"]
                            chan close $channel
                        } } $channel]
            } }] -myaddr localhost 0]
    lassign [chan configure $sock -sockname] address hostname port
    set discord::ApiBaseUrl "http://${hostname}:$port"
} -body {
    discord::gateway::GetGateway
} -cleanup {
    set discord::ApiBaseUrl $originalUrl
    chan close $sock
} -returnCodes error -result {key "url" not known in dictionary}
